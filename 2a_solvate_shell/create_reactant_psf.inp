* Based on 2agy_sol_ion.pdb created by KR, found in curie/kr8728/AADH/NEW_MD
* VMD removed the solvated ions and water but kept the crystal water and protein
* NEW PSF and PDB created using the CHARMM 36 forcefield
* Adds the water shell from Grubmuller's Solvate program

prnlev 7
set datadir ../common
stream @datadir/toppar_water_ions_fixed.str
read rtf card name @datadir/top_all36_prot.rtf append flex
read rtf card name @datadir/aadh_topologies.rtf append flex
read para card name @datadir/par_all36_prot_mod.prm append flex
read para card name @datadir/aadh_parameters.prm append flex

read sequ pdb firstresid 73 name @datadir/B.pdb
gene B setup first none last cter

read sequ pdb firstresid 71 name @datadir/D.pdb
gene D setup first none last cter

read sequ pdb firstresid 73 name @datadir/A.pdb
gene A setup first none last cter

read sequ pdb firstresid 71 name @datadir/H.pdb
gene H setup first none last cter

patch disu D 75 D 140  setup
patch disu D 81 D 113 setup
patch disu D 88 D 171 setup
patch disu D 90 D 138 setup
patch disu D 91 D 135 setup
patch disu D 98 D 129 setup
patch disu D 130 D 161 setup
patch disu H 75 H 140 setup
patch disu H 88 H 171 setup
patch disu H 90 H 138 setup
patch disu H 91 H 135 setup
patch disu H 98 H 129 setup
patch disu H 130 H 161 setup
patch disu A 225 A 242 setup
patch disu B 225 B 242 setup

autogenerate off !In KR original (C22?) the autogen feature was off by default. 
                    ! in Charmm 36 the autogen feature is on - which is good for 
                    ! the disu but the patches below were especially crafted so they
                    ! specify the angles and dihedrals.
                    ! in particular the newly formed (TTW:C)-(TRP:C)-(TRP:N) was left out ( why? )
bomlev -2 ! To avoid partial charge warning. 
! For some reason the first two patches work in KR's original with the preservation of charge
! but not in this case - perhaps she changed it.  Doesn't actually matter
! as the change in charges introduced by the first two patches get's altered again 
! in the last two charges.  
patch ttq_trp D 109 D 160 setup
patch ttq_trp H 109 H 160 setup
patch trpm D 160 setup
patch trpm H 160 setup

bomlev 0

!
! READ IN WATER 
!
read sequ pdb name @datadir/XW1.pdb
gene XW1 setup first none last none nodihe noangl
read sequ pdb name @datadir/XW2.pdb
gene XW2 setup first none last none nodihe noangl
read sequ pdb name @datadir/XW3.pdb
gene XW3 setup first none last none nodihe noangl
read sequ pdb name @datadir/XW4.pdb
gene XW4 setup first none last none nodihe noangl
read sequ pdb name "WAT1.pdb"
gene WAT1 setup first none last none nodihe noangl
read sequ pdb name "WAT2.pdb"
gene WAT2 setup first none last none nodihe noangl
read sequ pdb name "WAT3.pdb"
gene WAT3 setup first none last none nodihe noangl

!
! READ IN COORDINATES
!
read coor pdb resid name @datadir/A.pdb sele segid A end
read coor pdb resid name @datadir/D.pdb sele segid D end
read coor pdb resid name @datadir/B.pdb sele segid B end
read coor pdb resid name @datadir/H.pdb sele segid H end
read coor pdb resid name @datadir/XW1.pdb sele segid XW1 end
read coor pdb resid name @datadir/XW2.pdb sele segid XW2 end
read coor pdb resid name @datadir/XW3.pdb sele segid XW3 end
read coor pdb resid name @datadir/XW4.pdb sele segid XW4 end
read coor pdb resid name "WAT1.pdb" sele segid WAT1 end
read coor pdb resid name "WAT2.pdb" sele segid WAT2 end
read coor pdb resid name "WAT3.pdb" sele segid WAT3 end

!
! WRITE OUT THE PROTEIN AND WATER
!
write psf card name ./2agy_c36_state0_ws.psf
write coor pdb name ./2agy_c36_state0_ws.pdb
write coor card name ./2agy_c36_state0_ws.cor

stop
