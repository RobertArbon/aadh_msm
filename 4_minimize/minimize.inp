* Minimisation
*

prnlev 5

set molname 2agy_c36_state0_neut
set datadir ../common
stream @datadir/toppar_water_ions_fixed.str
read rtf card name @datadir/top_all36_prot.rtf append flex
read rtf card name @datadir/aadh_topologies.rtf append flex
read para card name @datadir/par_all36_prot_mod.prm append flex
read para card name @datadir/aadh_parameters.prm append flex

read psf card name ../common/@molname.psf
read coord card name ../common/@molname.crd

define heavy_all sele all .and. .not. type H* end

define protein sele segid AL* .or. segid BT* end
define heavy_prot sele protein .and. .not. type H* end 
define light_prot sele protein .and. type H* end 

define backbone_prot sele type n .or. type ca .or. type c .or. type o end
define residues_prot sele protein .and. .not. backbone_prot end

define water sele segid *WAT end
define ions sele segid ion end

!
! Orient the cell so that the origin is a corner, not the centre.
!
coordinate statistics select all end

calc xdim = abs ( ?XMAX - ?XMIN )
calc ydim = abs ( ?YMAX - ?YMIN ) 
calc zdim = abs ( ?ZMAX - ?ZMIN )

coor trans xdir @xdim ydir @ydim zdir @zdim factor 0.5
!
! Check that reorientation has worked
!
coordinate statistics select all end
!
! Setup crystal
!
calc xcen @xdim/2
calc ycen @ydim/2
calc zcen @zdim/2

set cutim 14

CRYSTAL DEFI ORTHS @xdim @ydim @zdim 90.0 90.0 90.0
CRYSTAL BUILD CUTOFF @cutim NOPER 0
image byseg xcen @xcen ycen @ycen zcen @zcen  sele segid AL* .or. segid BT* end ! protein
image byres xcen @xcen ycen @ycen zcen @zcen sele .not. (segid AL* .or. segid BT*) end ! water ions

! 
! Write out crystal parameters
!
open write card unit 20 name cyrstal_params.str

write title card unit 20
* Crystal parameters from Minimization:
* set xdim @xdim
* set ydim @ydim
* set zdim @zdim
* set xcen @xcen
* set xcen @ycen
* set xcen @zcen
* set cutim @cutim 
*

!
! Setup non-bonded forces
!
set ftx 128
set fty 108
set ftz 90
NBONDED inbfrq -1 imgfrq -1 nbxmod 5 -                                              ! C36 DEFAULTS
        atom cdiel fshift -                                                         ! use atom based electrostatics, C36 DEFAULTS
        vatom vdistance vfswitch -                                                  ! C36 DEFAULTS
        elec ewald pmew fftx @ftx ffty @fty fftz @ftz kappa .34 spline order 6 -    ! PME SUMMATION
        cutnb 14.0 ctofnb 12.0 ctonnb 10.0 -                                        ! C36 DEFAULTS
        cutim @cutim -                                                                ! must be same as cutnb
        eps 1.0 e14fac 1.0 wmin 1.5                                                 ! C36 DEFAULTS

faster on

!
! Minimization 
!
open write card unit 30 name minimization_traj.txt

coor copy comp 
energy 


write title card unit 30
* Step, Energy, RMSD
* 0, ?ENER,?RMS
 
!
! Step 1: Minimize all waters 
!
cons rmsd force 5 mass comp sele protein end
 
mini sd nstep 100
mini abnr nstep 3000 nprint 100 tolg 0.01

coor rms comp sele water .or. ions end
cons clear
ener
coor rmsd comp sele all end
write title card unit 30
* 1, ?ENER,?RMS

!
! Step 2: Minimize all hydrogens and waters 
!
cons rmsd force 5 mass comp sele protein .and. .not. prothyd end 

mini sd nstep 100
mini abnr nstep 3000 nprint 100 tolg 0.01

coor rmsd comp sele water .or. ions end
coor rmsd comp sele prothyd end
cons clear
ener
coor rms comp sele all end
write title card unit 30
* 2, ?ENER,?RMS

!
! Step 3: Minimize residues + hydrogens + waters
! 
cons rmsd force 5 mass comp sele backbone end 

mini sd nstep 100
mini abnr nstep 3000 nprint 100 tolg 0.01

coor rmsd comp sele water .or. ions end
coor rmsd comp sele prothyd end
coor rmsd comp sele protein .and. .not backbone end
cons clear
ener
coor rmsd comp sele all end
write title card unit 30
* 3, ?ENER,?RMS

!
! Write out coordinates
!
write coor card name 2agy_final_min.crd
* 2agy, minimized, neutralized
*

stop
