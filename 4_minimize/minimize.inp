* Minimisation
*

prnlev 5
set molname 2agy_solv_ion
set datadir ../common
stream @datadir/toppar_water_ions_fixed.str
read rtf card name @datadir/top_all36_prot.rtf append flex
read rtf card name @datadir/aadh_topologies.rtf append flex
read para card name @datadir/par_all36_prot_mod.prm append flex
read para card name @datadir/aadh_parameters.prm append flex

read psf card name ../common/@molname.psf
read coord card name ../common/@molname.cor

!
! Some selections
!
define protein sele segid AL* .or. segid BT* end
define water sele segid *WAT end
define ions sele segid ion end

!
! Orient the cell so that the origin is a corner, not the centre for compatability with OpenMM.
!
coordinate statistics select all end

calc xdim = abs ( ?XMAX - ?XMIN )
calc ydim = abs ( ?YMAX - ?YMIN ) 
calc zdim = abs ( ?ZMAX - ?ZMIN )

coor trans xdir @xdim ydir @ydim zdir @zdim factor 0.5

!
! Setup crystal
!
calc cen @xdim/2

set cutim 14

crystal defi cubic @xdim @xdim @xdim 90.0 90.0 90.0
crystal build cutoff @cutim noper 0
image byseg xcen @cen ycen @cen zcen @cen sele protein end 
image byres xcen @cen ycen @cen zcen @cen sele water .or. ions end 

open write card unit 11 name aadh.xtl
write crystal card unit 11

!
! Delete extraneous waters
!
coor copy comp
wrnlev 1
update inbfrq 0
coor diff
define sel1 select .byres. (property x .ne. 0.0 .or. property y .ne. 0.0 .or. property z .ne. 0.0) show end
coor swap
bomlev -1
delete atom sele sel1 end
bomlev 0

!
! Define selections
!
define heavy_all sele all .and. .not. type H* end
define protein sele segid AL* .or. segid BT* end
define heavy_prot sele protein .and. .not. type H* end 
define light_prot sele protein .and. type H* end 
define backbone_prot sele type n .or. type ca .or. type c .or. type o end
define residues_prot sele protein .and. .not. backbone_prot end
define water sele segid *WAT end
define ions sele segid ion end

!
! WRITE OUT FINAL PSF and COORDS
!
write coord card name 2agy_final.cor
* Realigned the Crystal structure to be compatible with OpenMM
* Cubic crystal structure box size: @xdim Ang 
* Not minimized
*

write coord pdb name 2agy_final.pdb
* Realigned the Crystal structure to be compatible with OpenMM
* Cubic crystal structure box size: @xdim Ang 
* Not minimized
*

write psf card name 2agy_final.psf
* Realigned the Crystal structure to be compatible with OpenMM
* Cubic crystal structure box size: @xdim Ang 
* Not minimized
*
!
! Re-setup crystal
!
crystal free
crystal defi cubic @xdim @xdim @xdim 90.0 90.0 90.0
crystal build cutoff @cutim noper 0
image byseg xcen @cen ycen @cen zcen @cen sele protein end
image byres xcen @cen ycen @cen zcen @cen sele water .or. ions end

!
! Setup non-bonded forces
!
set ftx 144 
set fty 144
set ftz 144 
NBONDED inbfrq -1 imgfrq -1 nbxmod 5 -                                              ! C36 DEFAULTS
        atom cdiel fshift -                                                         ! use atom based electrostatics, C36 DEFAULTS
        vatom vdistance vfswitch -                                                  ! C36 DEFAULTS
        elec ewald pmew fftx @ftx ffty @fty fftz @ftz kappa .34 spline order 6 -    ! PME SUMMATION
        cutnb 14.0 ctofnb 12.0 ctonnb 10.0 -                                        ! C36 DEFAULTS
        cutim @cutim -                                                                ! must be same as cutnb
        eps 1.0 e14fac 1.0 wmin 1.5                                                 ! C36 DEFAULTS

faster on

coor copy comp 

!
! Minimization 
!

coor rms comp sele heavy_prot end
 
!
! Step 1: Minimize H-atoms
!         Fix heavy atoms 
!
cons rmsd force 5 mass comp sele heavy_all end
 
mini sd nstep 100
mini abnr nstep 3000 nprint 100 tolg 0.01

cons rmsd clear
coor rms comp sele heavy_prot end

write coor card name 2agy_final_min-step-1.cor
* 2agy, minimized, neutralized after step 1
*

!
! Step 2: Minimize all hydrogens and waters
!         Fix all heavy protein atoms 
!
cons rmsd force 5 mass comp sele heavy_prot end 

mini sd nstep 100
mini abnr nstep 3000 nprint 100 tolg 0.01

cons rmsd clear
coor rms comp sele all end


write coor card name 2agy_final_min-step-2.cor
* 2agy, minimized, neutralized after step 2
*

!
! Step 3: Minimize residues + hydrogens + waters
! 
cons rmsd force 5 mass comp sele  backbone_prot end 

mini sd nstep 100
mini abnr nstep 3000 nprint 100 tolg 0.01

cons rmsd clear
coor rms comp sele heavy_prot end

write coor card name 2agy_final_min-step-3.cor
* 2agy, minimized, neutralized after step 3
*

!
! Step 4: Minimize everything  
! 
mini sd nstep 100
mini abnr nstep 5000 nprint 100 tolg 0.01

coor rms comp sele heavy_prot end

!
! Write out coordinates
!
write coor card name 2agy_final_min.cor
* 2agy, minimized

write coor pdb name 2agy_final_min.pdb
* 2agy, minimized
*

stop
