* Equilibration in stages 
*

envi OPENMM_PLATFORM  "CUDA"   ! /CUDA/Reference   Use OpenMM platform based on
envi OPENMM_PRECISION "mixed"   ! /mixed/double    Do calculations in single

prnlev 5
set datadir ../common
stream @datadir/toppar_water_ions_fixed.str
read rtf card name @datadir/top_all36_prot.rtf append flex
read rtf card name @datadir/aadh_topologies.rtf append flex
read para card name @datadir/par_all36_prot_mod.prm append flex
read para card name @datadir/aadh_parameters.prm append flex

read psf card name ../common/2agy_c36_state0_neut.psf
read coord card name ../common/2agy_solv_min.cor

define backbone_prot sele type n .or. type ca .or. type c .or. type o end

stream crystal_params.str

CRYSTAL DEFI ORTHS @xdim @ydim @zdim 90.0 90.0 90.0
CRYSTAL BUILD CUTOFF @cutim NOPER 0
image byseg xcen @xcen ycen @ycen zcen @zcen  sele segid AL* .or. segid BT* end ! protein
image byres xcen @xcen ycen @ycen zcen @zcen sele .not. (segid AL* .or. segid BT*) end ! water ions

!
! Setup non-bonded forces
!
set ftx 144 
set fty 120
set ftz 108 
NBONDED inbfrq -1 imgfrq -1 nbxmod 5 -                                              ! C36 DEFAULTS
        atom cdiel fshift -                                                         ! use atom based electrostatics, C36 DEFAULTS
        vatom vdistance vfswitch -                                                  ! C36 DEFAULTS
        elec ewald pmew fftx @ftx ffty @fty fftz @ftz kappa .34 spline order 6 -    ! PME SUMMATION
        cutnb 14.0 ctofnb 12.0 ctonnb 10.0 -                                        ! C36 DEFAULTS
        cutim @cutim -                                                                ! must be same as cutnb
        eps 1.0 e14fac 1.0 wmin 1.5                                                 ! C36 DEFAULTS

faster on
omm on

shake fast bonh tol 1.0e-8 para

set echeck = echeck -1

set nsteps 5000
set nwrite 100
set gamma 1

coor copy comp 
!
! Heating loop
!
energy omm 

set tstart 10
set tend 310
set tinc 25

set tcurr @tstart

cons harm absolute expo 2 force 10 sele backbone_prot end mass comp 

label loop

    if @tcurr .eq. @tstart then
         set startcon start
         set readunit -1
    endif
    if @tcurr .gt. @tstart then 
        set startcon restart
        set readunit 10
        open read card unit 10 name 2agy-heating-@told.res
    endif

    open write card unit 11 name  2agy-heating-@tcurr.res
     
    dynamics leap @startcon timestep 0.002 -
         nstep @nsteps nprint @nwrite iprfrq @nwrite isvfrq @nsteps iunwri 11 iunread @readunit -
         firstt @tcurr finalt @tcurr  -
         ichecw 0 ihtfrq 0 ieqfrq 0 -
         iasors 1 iasvel 1 iscvel 0  -
         ilbfrq 0 inbfrq -1 imgfrq -1 @echeck bycb -
         ntrfq 100 -
         omm langevin gamma @gamma 
    
    coor rms copy sele backbone_prot end
    coor rms copy sele segid BT* .or. segid AL* end    

    write coor card name 2agy-heating-@tcurr.cor
    * 2agy intermediate output.  Heated to @tcurr K
    *
    
 
    set told @tcurr 
    calc tcurr = @tcurr + @tinc
    if @tcurr .le. @tend goto loop
  

!
! Write out coordinates
!
write coor card name 2agy_heated.cor
* 2agy, minimized, neutralized, heated to 310K
*

write coor pdb name 2agy_heated.cor
* 2agy, minimized, neutralized, heated to 310K
*

stop
