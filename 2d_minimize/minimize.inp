* Removes bad WDVs contacts from waters and truncates to orthorhombic crystal system
*

stream /users/robert_arbon/code/aadh/setup.inp

prnlev 5

ioformat extend

set molname 2agy_c36_state0_min_wb

stream @charmmtop/toppar_water_ions_fixed.str
read rtf card name @charmmtop/top_all36_prot.rtf append flex
read rtf card name @datadir/ra/aadh_topologies.rtf append flex
read para card name @charmmtop/par_all36_prot_mod.prm append flex
read para card name @datadir/ra/aadh_parameters.prm append flex

read psf card name ../common/@molname.psf
read coor card name ../common/@molname.crd

!
! SOME TIDY UP
join XW1 XW2 renumber
join XW1 XW3 renumber
join XW1 XW4 renumber

rename segid XWAT sele segid XW1 end

set ctr 5
label loop
  join WT4 WT@ctr renumber
  incr ctr 1
if @ctr .lt. 31.5 then goto loop

rename segid BWAT sele segid WT4 end

rename segid AL2 sele segid A end
rename segid AL1 sele segid B end

rename segid BT1 sele segid D end
rename segid BT2 sele segid H end

join AL1 renumber
join AL2 renumber
join BT1 renumber
join BT2 renumber 


write psf card name @molname.psf
* @molname relabelled and residue sorted
*

write coord card name @molname.crd
* @molname relabelled and residue sorted
*

write coord pdb name @molname.pdb
* @molname relabelled and residue sorted
*

! SELECTIONS
define prot sele segid AL* .or. segid BT* end
define mainc sele (type n .or. type ca .or. type c .or. type o) .and. (segid AL* .or. segid BT*) end
define water sele segid *wat end

! TRUNCATE SYSTEM TO ORTHORHOMBIC CRYSTAL
calc buffer 2*12.0

coordinate statistics selection prot end

calc xdim = ( abs ( ?XMAX - ?XMIN ) + @buffer )
calc ydim = ( abs ( ?YMAX - ?YMIN ) + @buffer )
calc zdim = ( abs ( ?ZMAX - ?ZMIN ) + @buffer )

! coordinate statistics selection all end


! ! Create sphere to circumscribe a cube of @xdim
! calc gvsq = @xdim * @xdim
! calc safesphere = sqrt( 3 * @gvsq )
! calc safesphere = @safesphere/2
! delete atom sort select .byres. ( .not. ( point 0. 0. 0. cut @safesphere ) .and. ( segid *wat ) ) end

! Define crystal
crystal define orth @xdim @ydim @zdim 90.0 90.0 90.0
crystal build noper 0
image byres sele segid *wat end

coor copy comp

! set wrnlev down to reduce clutter...
wrnlev 1

! Update the image lists. If any atoms move during this process, it means that they are extraneous
! to the crystal structure that we want to build and should be deleted. Therefore, coor diff is used to
! detect moving atoms, which had selected for deletion.
update inbfrq 0

coor diff

define extra select .byres. (property x .ne. 0.0) .or. (property y .ne. 0.0) .or. (property z .ne. 0.0) end

coor swap

! we need to set bomlev down, otherwise we get an error about modifying the PSF when image
! centering is active
bomlev -1
delete atom sele extra end
bomlev 0

! write psf card name tmp.psf
! * @molname relabelled, residue sorted and truncated
! * @xdim x @ydim x @zdim
! *

! write coord card name tmp.crd
! * @molname relabelled, residue sorted and truncated
! * @xdim x @ydim x @zdim
! *

! CONSTRAINTS
define mainc sele (type n .or. type ca .or. type c .or. type o) .and. (segid AL* .or. segid BT*) end

!shake bonh param sele all end
!cons harm sele mainc end force 0.5

! NONBOND OPTIONS 
NBONDED nbxmod  5 inbfrq -1 atom cdiel fshift vatom vdistance vfswitch -
cutnb 14.0 ctofnb 12.0 ctonnb 10.0 eps 1.0 e14fac 1.0 wmin 1.5 

coor copy comp

!MINIMIZATION
mini sd nstep 100 nprint 1 tolgrd 100.0

!CALCULATE RMS
coor rms sele mainc end
coor rms sele ( .not. type h* ) .and. ( .not. mainc ) end

ioform extended

!WRITE OUT STRUCTURE

write psf card name @molname_min.psf
* Minimized @molname to remove bad VdWs contacts
* Reoriented along x axis
* Truncated to box of size @xdim x @ydim x @zdim
*

write coord card name @molname_min.crd
* Minimized @molname to remove bad VdWs contacts
* Reoriented along x axis
* Truncated to box of size @xdim x @ydim x @zdim
*

write coord pdb name @molname_min.pdb
* Minimized @molname to remove bad VdWs contacts
* Reoriented along x axis
* Truncated to box of size @xdim x @ydim x @zdim
*

open unit 50 write card name crystal.xtl
cryst write card unit 50
* @xdim x @ydim x @zdim
* orthorhombic
*

open unit 10 write card name build_crystal.str
write title unit 10
* * Stream file for setting up cubic crystal. 
* SET XDIM @xdim
* SET YDIM @ydim
* SET ZDIM @zdim
* crystal defi orths @xdim @ydim @zdim 90.0 90.0 90.0
* crystal build noper 0
* image byres sele segid *wat end


stop

