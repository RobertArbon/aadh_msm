* Heat the HIS dimer to 310K
*

stream toppar_water_ions_fixed.str
read rtf card name top_all36_prot.rtf append flex
read para card name par_all36_prot.prm append flex

set molname his-dimer-final

read psf card name @molname.psf
read coor card name @molname.cor

set finalt 310

stream build_crystal.str
stream images.str
stream non_bond_spec.str

! Configure shake
shake bonh param sele all end

open unit 41 write card name @molname-@finalt.res !restart file that will be written
open unit 31 write file name @molname-@finalt.dcd !trajectory file name
open unit 21 write card name @molname-@finalt.ene

! dynamics command
dyna leap verlet start -                     ! use leap-frog verlet integrator
   timestep 0.001 nstep 100000 nprint 100 - ! run 100K steps @ 1 fs time-steps
   iunwri 41 iuncrd 31 kunit 21 nsavc 100 -           ! save a restart file to unit 41, write coordinates to thetrajectory every 100 steps
   firstt 110.0 finalt @finalt.0 tbath @finalt.0 -   ! heat from 110K to 310K (200 K) with a 310K bath
   ihtfrq 2500 teminc 5 ieqfrq 0 -           ! heat the system 5K every 2500 steps, no equilibration needed
   iasors 1 iasvel 1 iscvel 0 ichecw 0 -     ! assign velocities via a Gaussian distribution every IHTFRQ steps, do not check temperatures (always scale)
   ntrfrq 500 -                              ! stop rotation and translation every 500 steps
   iseed  11033 -                            ! pick a random seed for the velocity re-assignment
   echeck 100.0                              ! If energy changes more than 100 kcal/mol bail out


! write out the final coordinates
ioform extended

write coor card name @molname-@finalt.cor
* Coordinates after heating
*

stop
